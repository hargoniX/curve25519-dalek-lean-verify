/-
Copyright 2026 The Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Dablander
-/
import Curve25519Dalek.Funs
import Curve25519Dalek.Math.Ristretto.Representation
import Curve25519Dalek.Specs.Edwards.EdwardsPoint.Sub
import Utils.GrindBench

/-!
# Spec theorem for `curve25519_dalek::ristretto::RistrettoPoint::sub`

Subtracts two Ristretto points via elliptic curve group subtraction. The implementation unwraps
both points to their underlying `EdwardsPoint` representations, performs Edwards subtraction,
and wraps the result back as a `RistrettoPoint`.

Source: "curve25519-dalek/src/ristretto.rs"
-/

open Aeneas Aeneas.Std Result Aeneas.Std.WP
open curve25519_dalek.ristretto
namespace curve25519_dalek.Shared0RistrettoPoint.Insts
namespace CoreOpsArithSubSharedARistrettoPointRistrettoPoint

/-- **Spec theorem for `curve25519_dalek::ristretto::RistrettoPoint::sub`**
• The function always succeeds (no panic) for valid inputs
• The result is a valid Ristretto point
• The result represents the difference of the inputs (via elliptic curve group subtraction)
-/
@[step]
theorem sub_spec (self other : RistrettoPoint) (h_self_valid : self.IsValid)
    (h_other_valid : other.IsValid) :
    sub self other ⦃ (result : RistrettoPoint) =>
      result.IsValid ∧
      result.toPoint = self.toPoint - other.toPoint ⦄ := by
  unfold sub edwards.EdwardsPoint.Insts.CoreOpsArithSubEdwardsPointEdwardsPoint.sub
  step
  · have h_toPoint : RistrettoPoint.toPoint ep = self.toPoint - other.toPoint := by
      unfold RistrettoPoint.toPoint; exact ep_post2
    have h_even : ∀ r : RistrettoPoint, r.IsValid → IsEven r.toPoint := fun r hr => by
      unfold RistrettoPoint.toPoint; exact (EdwardsPoint_IsSquare_iff_IsEven r hr.1).mp hr.2
    refine ⟨⟨ep_post1, ?_⟩, h_toPoint⟩
    rw [EdwardsPoint_IsSquare_iff_IsEven ep ep_post1]
    unfold RistrettoPoint.toPoint at h_toPoint
    rw [h_toPoint, sub_eq_add_neg]
    apply even_add_closure_Ed25519
    · exact h_even self h_self_valid
    · obtain ⟨Q, hQ⟩ := (IsEven_iff_in_doubling_image _).mp (h_even other h_other_valid)
      exact (IsEven_iff_in_doubling_image _).mpr ⟨-Q, by
        unfold RistrettoPoint.toPoint at hQ; rw [hQ]; abel⟩

end CoreOpsArithSubSharedARistrettoPointRistrettoPoint
end curve25519_dalek.Shared0RistrettoPoint.Insts
